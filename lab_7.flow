import string;
import lingo/pegcode/driver;

Expr ::= IntConst, Mul, Add, Sub, Div, Neg, Var;
IntConst: (value: int);
Mul: (left: Expr, right: Expr);
Add: (left: Expr, right: Expr);
Sub: (left: Expr, right: Expr);
Div: (left: Expr, right: Expr);
Neg: (value: Expr);
Var: (name: string);

Rational(n: int, d: int);

int2rat(i: int) -> Rational {
    Rational(i, 1);
}

ints2rat(n: int, d: int) -> Rational {
    Rational(n, d);
}

rat2str(r: Rational) -> string {
    if (r.d == 1) i2s(r.n)
    else i2s(r.n) + "|" + i2s(r.d);
}

str2expr(str: string) -> Expr {
    lng = "#include D:\flow9\sandbox\mtk\lab_7.lingo";
    parsic(compilePegGrammar(lng), str, defaultPegActions);
}

expr2str(expr: Expr) -> string {
    switch (expr) {
        IntConst(c): i2s(c);
        Mul(l, r): "("  + expr2str(l) + "*" + expr2str(r) + ")";
        Add(l, r): "("  + expr2str(l) + "+" + expr2str(r) + ")";
        Sub(l, r): "("  + expr2str(l) + "-" + expr2str(r) + ")";
        Div(l, r): "("  + expr2str(l) + "/" + expr2str(r) + ")";
        Neg(v): "("  + "-" + expr2str(v) + ")";
        Var(n): n;
    }
}

MulRat(l: Rational, r: Rational) -> Rational {
    x = l.n * r.n;
    y = l.d * r.d;
    ints2rat(x / gcd(x, y), y / gcd(x,y));
}

AddRat(l: Rational, r: Rational) -> Rational {
    x = l.n * r.d + l.d * r.n;
    y = l.d * r.d;
    ints2rat(x / gcd(x, y), y / gcd(x,y));
}

SubRat(l: Rational, r: Rational) -> Rational {
    x = l.n * r.d - l.d * r.n;
    y = l.d * r.d;
    ints2rat(x / gcd(x, y), y / gcd(x,y));
}

DivRat(l: Rational, r: Rational) -> Rational {
    x = l.n * r.d;
    y = l.d * r.n;
    ints2rat(x / gcd(x, y), y / gcd(x,y));
}

NegRat(v: Rational) -> Rational {
    ints2rat(-v.n, v.d);
}

maybeOp(f: (?, ??) -> ???, arg1: Maybe<?>, arg2: Maybe<??>) -> Maybe<???> {
    switch (arg1) {
        None(): None();
        Some(x): switch (arg2) {
            None(): None();
            Some(y): Some(f(x, y));
        }
    }
}

Calc(expr: Expr, value: [Pair<string, int>]) -> Maybe<Rational> {
    switch (expr) {
        IntConst(c): Some(int2rat(c));
        Mul(l, r): maybeOp(MulRat, Calc(l, value), Calc(r, value));
        Add(l, r): maybeOp(AddRat, Calc(l, value), Calc(r, value));
        Sub(l, r): maybeOp(SubRat, Calc(l, value), Calc(r, value));
        Div(l, r): maybeOp(DivRat, Calc(l, value), Calc(r, value));
        Neg(v): maybeMap(Calc(v, value), NegRat);
        Var(n): {
            index = findi(value, \p -> p.first == n);
            switch (index) {
                None(): None();
                Some(i): Some(int2rat(value[i].second));
            }
        }
    }
}

main() {
    x = "x";
    a = "a";

    value = [Pair(x, 5), Pair(a, 10)];
    expr = Add(Mul(Mul(Var(x), Var(x)), Var(a)), Neg(Add(Var(x), IntConst(42))));

    expr_str = expr2str(expr);
    println("Test 1:\nExpression: " + expr_str);

    switch (Calc(expr, value)) {
		Some(result): println("Result: " + rat2str(result));
		None(): println("NONE");
	}

    println("");
    value_1 = [Pair("a", 2), Pair("b", 3)];
    expr_str_1 = "((32/(10*a))-((b*((7-(a*b))/6))/((b+b)/(9-(6/a)))))";
    println("Test 2:\nExpression: " + expr_str_1);
	expr_1 = str2expr(expr_str_1);

	switch (Calc(expr_1, value_1)) {
		Some(result_1): println("Result: " + rat2str(result_1));
		None(): println("NONE");
	}

     quit(0);
}